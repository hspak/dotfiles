[[ $- != *i* ]] && return ]]
(( UID == 0 )) && isroot=true || isroot=false

#fpath=($HOME/.zsh/completion $fpath )

# Turn autocomplete on
autoload -U compinit edit-command-line vcs_info zmv
compinit
zle -N edit-command-line

# history
HISTFILE=~/.zhistory
HISTSIZE=1500
SAVEHIST=1000

setopt nobeep \
    notify \
    nobgnice \
    correct \
    interactivecomments \
    printexitvalue \
    autocd \
    autopushd \
    pushdtohome \
    chaselinks \
    histverify \
    histappend \
    sharehistory \
    histreduceblanks \
    histignorespace \
    histignorealldups \
    histsavenodups \
    braceccl \
    dotglob \
    extendedglob \
    numericglobsort \
    nolisttypes \
    promptsubst \
    completealiases

# Enable vi mode
bindkey -v

# Enable setting terminal titles
case $TERM in
  xterm*|rxvt*|screen*|gnome*)
     precmd() {
       print -Pn "\e]0;%m:%~\a" 
       vcs_info
     }
     preexec () { print -Pn "\e]0;$1\a" }
    ;;
esac

# completion style {{{
# menu completion
zstyle ':completion:*' menu select

# colors for file completion
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

# complete all processes
zstyle ':completion:*:processes' command 'ps -e'
zstyle ':completion:*:processes-names' command 'ps -eo comm'

# cache completion
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path /tmp

# don't complete working directory in parent
zstyle ':completion:*' ignore-parents parent pwd
# }}}

# title (for vte, xterm and rxvt) {{{
case "$TERM" in
    vte*|xterm*|rxvt*)
        update_title() { print -Pn '\e];%n (%~) - Terminal\a' } ;;
    *)
        update_title() {} ;;
esac
# }}}

# prompt {{{
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '%F{red}S%F{white}'
zstyle ':vcs_info:*' unstagedstr '%F{red}U%F{white}'
zstyle ':vcs_info:git*' formats "%F{blue}%s%F{white}:%F{green}%b%F{white} %m%u%c% "

if $isroot; then
    PROMPT='%B%F{magenta} ┌┤%b%F{white}%n%F{cyan}@%F{white}%m%b %B%F{blue}%~ ${vcs_info_msg_0_}
 %F{magenta}└╼%b%f '
else
    PROMPT='%B%F{black} ┌┤%b%F{grey}%n%F{cyan}@%F{grey}%m%b %B%F{blue}%~ ${vcs_info_msg_0_}
 %F{black}└╼%b%f '
fi
# }}}

# Environmental
[[ -n "$TMUX" ]] && export TERM=screen-256color
export EDITOR=vim
export PAGER=less
export PATH="$PATH:/usr/local/MATLAB/MATLAB_Production_Server/R2013a/bin:"

# External Config
if [[ -f ~/.dircolors ]] && type -p dircolors >/dev/null; then
    eval $(dircolors -b "$HOME/.dircolors")
fi

for snippets ($HOME/.zsh/*.zsh(N))
  source $snippets

# Allow backspace past insert position in vi-mode
zle -A .backward-kill-word vi-backward-kill-word
zle -A .backward-delete-char vi-backward-delete-char

unset isroot
